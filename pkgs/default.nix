# Applied as the `additions` overlay (last, see flake.nix) with the final package
# set (it refers to its own attrs) and the previous one as `prev`.
#
# The wrapped packages shadow the nixpkgs ones, so `pkgs.zellij` etc. are the
# wrapped versions. Each wrapper gets its upstream package from `prev`, passed
# explicitly under its own name (callPackage would inject the wrapped one and
# recurse).
{
  pkgs,
  prev,
}: {
  zellij = pkgs.callPackage ./zellij {zellij = prev.zellij;};
  hyprland = pkgs.callPackage ./hyprland {hyprland = prev.hyprland;};
  hyprlock = pkgs.callPackage ./hyprlock {hyprlock = prev.hyprlock;};
  hypridle = pkgs.callPackage ./hypridle {hypridle = prev.hypridle;};
  hyprpaper = pkgs.callPackage ./hyprpaper {hyprpaper = prev.hyprpaper;};
  rofi = pkgs.callPackage ./rofi {rofi = prev.rofi;};
  dunst = pkgs.callPackage ./dunst {dunst = prev.dunst;};
  jujutsu = pkgs.callPackage ./jujutsu {jujutsu = prev.jujutsu;};
  waybar = pkgs.callPackage ./waybar {waybar = prev.waybar;};
  kanshi = pkgs.callPackage ./kanshi {kanshi = prev.kanshi;};
  signal-desktop = pkgs.callPackage ./signal-desktop {signal-desktop = prev.signal-desktop;};
  glab = pkgs.callPackage ./glab {glab = prev.glab;};
  # No nixpkgs `claude` (it wraps `claude-code`). The nulls pin the "use the
  # bundled default" arguments so a same-named top-level package (e.g. `skills`)
  # is never auto-injected by callPackage.
  claude = pkgs.callPackage ./claude {
    instructions = null;
    agents = null;
    skills = null;
    settings = null;
  };

  # Standalone tools. claude takes knowledge, claude-hooks, claude-statusline
  # and jj-upload from the package set, so overrides of them propagate.
  # jj-upload needs the upstream jj: the wrapped one bundles jj-upload (recursion).
  jj-upload = pkgs.callPackage ./jj-upload {jujutsu = prev.jujutsu;};
  jw = pkgs.callPackage ./jw {};
  bitwarden-cli = pkgs.callPackage ./bitwarden-cli {bitwarden-cli = prev.bitwarden-cli;};
  wallpaper = pkgs.callPackage ./wallpaper {};
  agent-skills = pkgs.callPackage ./agent-skills {};
  claude-hooks = pkgs.callPackage ./claude-hooks {};
  claude-statusline = pkgs.callPackage ./claude-statusline {};
  knowledge = pkgs.callPackage ./knowledge {};

  event-handlers = {
    notify = pkgs.callPackage ./event-handlers/notify {};
    notify-critical = pkgs.event-handlers.notify.override {urgency = "critical";};
    knowledge = pkgs.callPackage ./event-handlers/knowledge {};
    # GitLab event -> Claude session in a zellij tab. No gitlab.* routes here (no
    # GitLab watcher): a layer that has one sets contextTool and adds the routes.
    claude = pkgs.callPackage ./event-handlers/claude {
      notify = pkgs.event-handlers.notify;
    };
  };

  # `handlers` stays explicit so `.override (old: {handlers = old.handlers // ...})`
  # can extend it.
  event-router = pkgs.callPackage ./event-router {
    handlers = {
      default = pkgs.event-handlers.notify;
      knowledge = pkgs.event-handlers.knowledge;
      # exact type wins over the `knowledge` prefix
      "knowledge.review_needed" = pkgs.event-handlers.notify;
    };
  };
}
