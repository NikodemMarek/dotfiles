{
  description = "system configuration";

  inputs = {
    nixpkgs.url = "nixpkgs/nixos-unstable";

    hardware.url = "github:nixos/nixos-hardware";

    sops-nix.url = "github:Mic92/sops-nix";

    # Overridden at build time by `secrets-build`, see devenv.nix
    secrets = {
      url = "path:./secrets-stub";
      flake = false;
    };

    disko = {
      url = "github:nix-community/disko";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    impermanence.url = "github:nix-community/impermanence";

    deploy-rs = {
      url = "github:serokell/deploy-rs";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    stylix = {
      url = "github:danth/stylix";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    hyprland = {
      url = "github:hyprwm/Hyprland";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    neovim.url = "github:NikodemMarek/neovim";

    ai.url = "github:NikodemMarek/ai";

    zen-browser = {
      url = "github:youwen5/zen-browser-flake";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs = {
    self,
    nixpkgs,
    systems,
    ...
  } @ inputs: let
    inherit (self) outputs;

    lib = nixpkgs.lib;

    forEachSystem = f: lib.genAttrs (import systems) (system: f pkgsFor.${system});
    pkgsFor = lib.genAttrs (import systems) (system:
      import nixpkgs {
        inherit system;
        config = {
          allowUnfree = true;
          allowUnfreePredicate = _: true;
        };
        overlays = lib.attrValues (import ./overlays {inherit inputs;});
      });
  in {
    inherit lib;

    nixosModules = import ./modules/host;

    overlays = import ./overlays {inherit inputs;};
    packages = forEachSystem (pkgs: pkgs.wrapped);

    nixosConfigurations = let
      mkHost = host: system:
        lib.nixosSystem {
          pkgs = pkgsFor.${system};
          specialArgs = {inherit inputs outputs;};
          modules = [./host/${host}];
        };
    in {
      yenn = mkHost "yenn" "x86_64-linux";
      geralt = mkHost "geralt" "x86_64-linux";
      roach = mkHost "roach" "x86_64-linux";
      regis = mkHost "regis" "x86_64-linux";
      triss = mkHost "triss" "aarch64-linux";
    };

    deploy.nodes = let
      mkNode = host: extra: let
        cfg = self.nixosConfigurations.${host};
      in
        {
          hostname = host;
          profiles.system = {
            user = "root";
            sshUser = "maintenance";
            interactiveSudo = true;
            path = cfg.pkgs.deploy-rs.lib.activate.nixos cfg;
          };
        }
        // extra;
    in {
      geralt = mkNode "geralt" {};
      roach = mkNode "roach" {};
      regis = mkNode "regis" {};
      triss = mkNode "triss" {};
    };

    checks = forEachSystem (pkgs:
      pkgs.deploy-rs.lib.deployChecks self.deploy);
  };
}
